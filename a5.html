<html>
  <head>
    <meta charset="UTF-8">
    <title>CPSC 314 Assignment 5 Sep 2018</title>
    <style>
      body { margin: 0; } canvas {width: 100%;
      height: 100% }
    </style>
  </head>
  <body>
    <div id="canvas"></div>
<!--    <script src="js/three.js"></script> -->
    <script src="js/three.min.js"></script>
    <script src="js/OBJLoader.js"></script>
    <script src="js/SourceLoader.js"></script>
    <script src="js/OrbitControls.js"></script>
    <script src="js/KeyboardState.js"></script> <!-- By Jerome Etienne: http://jetienne.com/ -->

<script id="floorVertShader" type="x-shader/x-vertex">
uniform vec2 uvScale;
varying vec2 vUv;
varying vec3 vcsNormal;
varying vec3 ocsPosition;
varying vec3 vcsPosition;
void main()
{       
  vUv = uv;
  ocsPosition = position;
  vec4 vcsP = modelViewMatrix * vec4(ocsPosition,1.0);
  vcsPosition = vcsP.xyz;
  gl_Position = projectionMatrix * vcsP;   // pass on point in CCS
  vcsNormal = normalMatrix * normal;       // normalMatrix is the transpose-inv of the rotation matrix
}
</script>

<script id="myVertShader" type="x-shader/x-vertex">
uniform vec2 uvScale;
varying vec2 vUv;
varying vec3 vcsNormal;
varying vec3 ocsPosition;
varying vec3 vcsPosition;
void main()
{       
  vUv = uvScale * uv;
  ocsPosition = position;
  vec4 vcsP = modelViewMatrix * vec4(ocsPosition,1.0);
  vcsPosition = vcsP.xyz;
  gl_Position = projectionMatrix * vcsP;   // pass on point in CCS
  vcsNormal = normalMatrix * normal;       // normalMatrix is the transpose-inv of the rotation matrix
}
</script>

<script id="skyBoxVertShader" type="x-shader/x-vertex">
varying vec2 vUv;
void main()
{       
  vec4 vcsP = modelViewMatrix * vec4(position,1.0);
  gl_Position = projectionMatrix * vcsP;   // pass on point in CCS
  vUv = uv;
}
</script>

<script id="magmaVertShader" type="x-shader/x-vertex">
  //!!!
  //
// GLSL textureless classic 3D noise "cnoise",
// with an RSL-style periodic variant "pnoise".
// Author:  Stefan Gustavson (stefan.gustavson@liu.se)
// Version: 2011-10-11
//
// Many thanks to Ian McEwan of Ashima Arts for the
// ideas for permutation and gradient selection.
//
// Copyright (c) 2011 Stefan Gustavson. All rights reserved.
// Distributed under the MIT license. See LICENSE file.
// https://github.com/ashima/webgl-noise
//
vec3 mod289(vec3 x)
{
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}
vec4 mod289(vec4 x)
{
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}
vec4 permute(vec4 x)
{
  return mod289(((x*34.0)+1.0)*x);
}
vec4 taylorInvSqrt(vec4 r)
{
  return 1.79284291400159 - 0.85373472095314 * r;
}
vec3 fade(vec3 t) {
  return t*t*t*(t*(t*6.0-15.0)+10.0);
}
// Classic Perlin noise
float cnoise(vec3 P)
{
  vec3 Pi0 = floor(P); // Integer part for indexing
  vec3 Pi1 = Pi0 + vec3(1.0); // Integer part + 1
  Pi0 = mod289(Pi0);
  Pi1 = mod289(Pi1);
  vec3 Pf0 = fract(P); // Fractional part for interpolation
  vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
  vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
  vec4 iy = vec4(Pi0.yy, Pi1.yy);
  vec4 iz0 = Pi0.zzzz;
  vec4 iz1 = Pi1.zzzz;
  vec4 ixy = permute(permute(ix) + iy);
  vec4 ixy0 = permute(ixy + iz0);
  vec4 ixy1 = permute(ixy + iz1);
  vec4 gx0 = ixy0 * (1.0 / 7.0);
  vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
  gx0 = fract(gx0);
  vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
  vec4 sz0 = step(gz0, vec4(0.0));
  gx0 -= sz0 * (step(0.0, gx0) - 0.5);
  gy0 -= sz0 * (step(0.0, gy0) - 0.5);
  vec4 gx1 = ixy1 * (1.0 / 7.0);
  vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
  gx1 = fract(gx1);
  vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
  vec4 sz1 = step(gz1, vec4(0.0));
  gx1 -= sz1 * (step(0.0, gx1) - 0.5);
  gy1 -= sz1 * (step(0.0, gy1) - 0.5);
  vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
  vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
  vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
  vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
  vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
  vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
  vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
  vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);
  vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
  g000 *= norm0.x;
  g010 *= norm0.y;
  g100 *= norm0.z;
  g110 *= norm0.w;
  vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
  g001 *= norm1.x;
  g011 *= norm1.y;
  g101 *= norm1.z;
  g111 *= norm1.w;
  float n000 = dot(g000, Pf0);
  float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
  float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
  float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
  float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
  float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
  float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
  float n111 = dot(g111, Pf1);
  vec3 fade_xyz = fade(Pf0);
  vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
  vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
  float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x);
  return 2.2 * n_xyz;
}
// Classic Perlin noise, periodic variant
float pnoise(vec3 P, vec3 rep)
{
  vec3 Pi0 = mod(floor(P), rep); // Integer part, modulo period
  vec3 Pi1 = mod(Pi0 + vec3(1.0), rep); // Integer part + 1, mod period
  Pi0 = mod289(Pi0);
  Pi1 = mod289(Pi1);
  vec3 Pf0 = fract(P); // Fractional part for interpolation
  vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
  vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
  vec4 iy = vec4(Pi0.yy, Pi1.yy);
  vec4 iz0 = Pi0.zzzz;
  vec4 iz1 = Pi1.zzzz;
  vec4 ixy = permute(permute(ix) + iy);
  vec4 ixy0 = permute(ixy + iz0);
  vec4 ixy1 = permute(ixy + iz1);
  vec4 gx0 = ixy0 * (1.0 / 7.0);
  vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
  gx0 = fract(gx0);
  vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
  vec4 sz0 = step(gz0, vec4(0.0));
  gx0 -= sz0 * (step(0.0, gx0) - 0.5);
  gy0 -= sz0 * (step(0.0, gy0) - 0.5);
  vec4 gx1 = ixy1 * (1.0 / 7.0);
  vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
  gx1 = fract(gx1);
  vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
  vec4 sz1 = step(gz1, vec4(0.0));
  gx1 -= sz1 * (step(0.0, gx1) - 0.5);
  gy1 -= sz1 * (step(0.0, gy1) - 0.5);
  vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
  vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
  vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
  vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
  vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
  vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
  vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
  vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);
  vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
  g000 *= norm0.x;
  g010 *= norm0.y;
  g100 *= norm0.z;
  g110 *= norm0.w;
  vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
  g001 *= norm1.x;
  g011 *= norm1.y;
  g101 *= norm1.z;
  g111 *= norm1.w;
  float n000 = dot(g000, Pf0);
  float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
  float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
  float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
  float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
  float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
  float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
  float n111 = dot(g111, Pf1);
  vec3 fade_xyz = fade(Pf0);
  vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
  vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
  float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x);
  return 2.2 * n_xyz;
}
	float stripes( float x, float f) {
		float PI = 3.14159265358979323846264;
		float t = .5 + .5 * sin( f * 2.0 * PI * x);
		return t * t - .5;
	}
	float turbulence( vec3 p ) {
		float w = 100.0;
		float t = -.5;
		for (float f = 1.0 ; f <= 10.0 ; f++ ){
			float power = pow( 2.0, f );
			t += abs( pnoise( vec3( power * p ), vec3( 10.0, 10.0, 10.0 ) ) / power );
		}
		return t;
	}
	float f( vec3 p ) {
		return pnoise( vec3( p ), vec3( 10.0, 10.0, 10.0 ) );
		return pnoise( 8.0 * vec3( p ), vec3( 10.0, 10.0, 10.0 ) );
	}
	varying vec3 vNormal;
	varying vec3 vReflect;
	varying float ao;
	uniform float time;
	uniform float weight;
	float fround( float value ) {
		return floor( value + 0.5 );
	}
	vec3 v3round( vec3 value ) {
		return vec3( fround( value.x ), fround( value.y ), fround( value.z ) );
	}

	void main() {
		vec3 evNormal = normal;
		vec3 aniNormal = 1.5 * evNormal + time;
		float f0 = weight * f( aniNormal );
		float fx = weight * f( aniNormal + vec3( .0001, 0.0, 0.0 ) );
		float fy = weight * f( aniNormal + vec3( 0.0, .0001, 0.0 ) );
		float fz = weight * f( aniNormal + vec3( 0.0, 0.0, .0001 ) );
		vec3 modifiedNormal = normalize( evNormal - vec3( (fx - f0) / .0001, (fy - f0) / .0001, (fz - f0) / .0001 ) );
		if( weight > 0.0 ) {
			ao = f0 / weight + 1.;
		} else {
			ao = 0.0;
		}
		vNormal = modifiedNormal;
		vec3 newPosition = position + f0 * evNormal;
		vec3 nWorld = normalize( mat3( modelMatrix[0].xyz, modelMatrix[1].xyz, modelMatrix[2].xyz ) * modifiedNormal );
		vReflect = -normalize( reflect( normalize( newPosition.xyz - cameraPosition ), nWorld ) );
		gl_Position = projectionMatrix * modelViewMatrix * vec4( newPosition, 1.0 );
	}
  </script>

<script id="holeyFragShader" type="x-shader/x-fragment">
uniform vec3 lightPosition;
uniform vec3 myColor;
varying vec3 vcsNormal;
varying vec3 vcsPosition;
varying vec3 ocsPosition;

void main( void ) {
  vec3 L = normalize(lightPosition - vcsPosition);
  vec3 N = normalize(vcsNormal);
  float i = max(0.0,dot(N,L));
  float scale = 4.0;

  vec3 ocsPosition_S = scale * ocsPosition; // scaled position
  float dx = ocsPosition_S.x - floor(ocsPosition_S.x + 0.5);
  float dy = ocsPosition_S.y - floor(ocsPosition_S.y + 0.5);
  float dz = ocsPosition_S.z - floor(ocsPosition_S.z + 0.5);
  float r2 = dx*dx + dy*dy + dz*dz; 

  if (r2 < 0.245) {
    discard;
  } else {
    gl_FragColor = vec4(myColor*i,1.0);
  }
}
</script>

<script id="toonFragShader" type="x-shader/x-fragment">
varying vec3 vcsNormal;
varying vec3 vcsPosition;
uniform vec3 lightPosition;
uniform vec3 myColor;

void main( void ) {
  vec3 L = normalize(lightPosition - vcsPosition);
  vec3 N = normalize(vcsNormal);

  float i = max(0.0,dot(N,L));
  i = floor(i*5.0)/5.0;

  //gl_FragColor = vec4(0.9,0.9,0.3,1.0);   // default is yellow
  gl_FragColor = vec4(myColor*i,1.0);
}
</script>

<script id="envmapFragShader" type="x-shader/x-fragment">
uniform vec3 lightPosition;
uniform sampler2D uPosxTexture;
uniform sampler2D uPosyTexture;
uniform sampler2D uPoszTexture;
uniform sampler2D uNegxTexture;
uniform sampler2D uNegyTexture;
uniform sampler2D uNegzTexture;
uniform vec3 myColor;
uniform mat4 matrixWorld;
varying vec3 vcsNormal;
varying vec3 ocsPosition;
varying vec3 vcsPosition;
void main( void ) {
  vec3 N = normalize(vcsNormal);
  vec3 I = vcsPosition;
  vec3 R = reflect(I,N);
  vec4 R_world = matrixWorld * vec4(R,1.0);
  float v = 0.0, u = 0.0;
  vec4 color = vec4(N,1.0);
  
  if(R_world.y >= abs(R_world.x) && R_world.y >= abs(R_world.z)) { //posY
    v = 0.5 * ((-R_world.z/R_world.y)+1.0);
    u = 0.5 * ((-R_world.x/R_world.y)+1.0);
    color = texture2D(uPosyTexture, vec2(u,v));
  } else if(R_world.x >= abs(R_world.y) && R_world.x >= abs(R_world.z)) { //posX
    v = 0.5 * ((R_world.y/R_world.x)+1.0);
    u = 0.5 * ((R_world.z/R_world.x)+1.0);
    color = texture2D(uPosxTexture, vec2(u,v));
  } else if(R_world.z >= abs(R_world.x) && R_world.z >= abs(R_world.y)) { //posZ
    v = 0.5 * ((R_world.y/R_world.z)+1.0);
    u = 0.5 * ((-R_world.x/R_world.z)+1.0);
    color = texture2D(uPoszTexture, vec2(u,v));
  } else if (R_world.y < -abs(R_world.x) && R_world.y < -abs(R_world.z)) { //negY
    v = 0.5 * ((-R_world.z/R_world.y)+1.0);
    u = 0.5 * ((R_world.x/R_world.y)+1.0);
    color = texture2D(uNegyTexture, vec2(u,v));
  } else if (R_world.x < -abs(R_world.y) && R_world.x < -abs(R_world.z)) { //negX
    v = 0.5 * (-(R_world.y/R_world.x)+1.0);
    u = 0.5 * ((R_world.z/R_world.x)+1.0);
    color = texture2D(uNegxTexture, vec2(u,v));
  } else {
    v = 0.5 * ((-R_world.y/R_world.z)+1.0);
    u = 0.5 * ((-R_world.x/R_world.z)+1.0);
    color = texture2D(uNegzTexture, vec2(u,v));
  }
  gl_FragColor = color;
}
</script>


<script id="floorFragShader" type="x-shader/x-fragment">
uniform vec3 lightPosition;
uniform vec3 myColor;
varying vec3 vcsPosition;
varying vec3 ocsPosition;
uniform sampler2D normalMap;
uniform sampler2D textureMap;
varying vec2 vUv;

void main( void ) {
  const float deg = -90.0;
  mat3 rotX = mat3(
    vec3(1.0,0.0,0.0),
    vec3(0.0,cos(radians(deg)),sin(radians(deg))),
    vec3(0.0,-sin(radians(deg)),cos(radians(deg))));

  vec3 texel = texture2D(normalMap, vUv).xyz;
  
  float nX = (texel.x*2.0) - 1.0;
  float nY = (texel.y*2.0) - 1.0;
  float nZ = (texel.z*2.0) - 1.0;
 
  vec3 N = normalize(rotX * vec3(nX,nY,nZ));
  vec3 L = normalize(lightPosition - vcsPosition);
  float i = max(0.0,dot(N,L));

  vec3 color = texture2D(textureMap, vUv).xyz;
  gl_FragColor = vec4(color*i,1.0);
}
</script>


<script id="pnoiseFragShader" type="x-shader/x-fragment">
float screenWidth = 2.0;
float PI = 3.1416;

// perlin noise code from: 
//   https://gist.github.com/patriciogonzalezvivo/670c22f3966e662d2f83

float rand(vec2 c){
	return fract(sin(dot(c.xy ,vec2(12.9898,78.233))) * 43758.5453);
}

float noise(vec2 p, float freq ){
	float unit = screenWidth/freq;
	vec2 ij = floor(p/unit);
	vec2 xy = mod(p,unit)/unit;
	//xy = 3.*xy*xy-2.*xy*xy*xy;
	xy = .5*(1.-cos(PI*xy));
	float a = rand((ij+vec2(0.,0.)));
	float b = rand((ij+vec2(1.,0.)));
	float c = rand((ij+vec2(0.,1.)));
	float d = rand((ij+vec2(1.,1.)));
	float x1 = mix(a, b, xy.x);
	float x2 = mix(c, d, xy.x);
	return mix(x1, x2, xy.y);
}

float pNoise(vec2 p, int res){
	float persistance = .5;
	float n = 0.;
	float normK = 0.;
	float f = 4.;
	float amp = 1.;
	int iCount = 0;
	for (int i = 0; i<50; i++){
		n+=amp*noise(p, f);
		f*=2.;
		normK+=amp;
		amp*=persistance;
		if (iCount == res) break;
		iCount++;
	}
	float nf = n/normK;
	return nf*nf*nf*nf;
}

varying vec3 ocsPosition;
varying vec3 vcsPosition;
void main( void ) {
  int levels = 1;
  float i=pNoise(ocsPosition.xy,levels);
  // (h) the noise changes as we rotate the camera around
  // When we increase the # of levels, the noise increases
  //float i=pNoise(vcsPosition.xy,levels);

  float j=floor(i+0.9); 
  gl_FragColor = vec4(j,j,0.3,1.0);

  //gl_FragColor = vec4(i,i,i,1.0);
}
</script>

<script id="pnoiseMagFragShader" type="x-shader/x-fragment">
  varying vec2 vUv;
	varying vec3 vNormal;
	varying vec3 vReflect;
	varying float ao;
	uniform sampler2D tShine;
	uniform float time;
	float PI = 3.14159265358979323846264;
	void main() {
		float yaw = .5 - atan( vReflect.z, - vReflect.x ) / ( 2.0 * PI );
		float pitch = .5 - asin( vReflect.y ) / PI;
		vec2 pos = vec2( yaw, pitch );
		vec3 color = texture2D( tShine, pos ).rgb;
		float diffuse_value1 = .0015 * max(dot(vNormal, vec3( -490.0, 29.8, -85.8 ) ), 0.0);
		float diffuse_value2 = .0005 * max(dot(vNormal, vec3( -460.0, 40.27, 187.4 ) ), 0.0);
		float diffuse_value3 = .0010 * max(dot(vNormal, vec3( 175.5, 30.04, 466.4 ) ), 0.0);
		float diffuse_value4 = .0005 * max(dot(vNormal, vec3( 466.0, 45.3, 172.9 ) ), 0.0);
	  gl_FragColor = vec4( color - .15 * ao + .5 * vec3( diffuse_value1 + diffuse_value2 + diffuse_value3 + diffuse_value4 ), 1.0 );
	}
  </script>

<script src="a5.js"></script>
</body>
</html>

